if (isBuildModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "kotlin-parcelize"
apply from: "../buildType.gradle"

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName


        consumerProguardFiles "consumer-rules.pro"

        // 1.ARouter
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }


    dataBinding {
        enabled true
    }

    viewBinding {
        enabled true
    }

    //配置清单文件
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']

            if (isBuildModule.toBoolean()) {
                //独立运行
                manifest.srcFile 'src/main/moon/AndroidManifest.xml'
            } else {
                //集成到宿主
                manifest.srcFile 'src/main/AndroidManifest.xml'
                resources {
                    //正式版本 排除 moon文件夹下的内容
                    exclude 'src/main/moon/*'
                }
            }
        }
    }

    resourcePrefix rootProject.ext.resourcePrefix['package_module_base']

}

dependencies {
    //2.ARouter
    kapt rootProject.ext.dependencies['arouter_compiler']

    api project(':library-base')
}